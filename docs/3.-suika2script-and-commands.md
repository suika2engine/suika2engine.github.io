---
layout: article
---

# 3. Suika2Script and Commands

Suika2 uses Suika2Script, a simple-to-use, feature-rich markup that acts as a game's building blocks.

## What is Suika2Script?
Suika2Script is the markup language used in the creation of Suika2 script files (often referred to as scenario files in a general sense). Being such a versatile markup, the user only needs to learn a few concepts before they are able to utilize the entire feature-set from displaying messages, images, playing sounds, music, and even building menus.

Suika2Script is an actively developed markup, therefore it is subject to changes alongside Suika2. Please refer to this document or the [official website's command reference](https://suika2.com/en/reference.html) for information regarding new, updated, and deprecated commands and terms.

## Commands
Commands are the instructions that make up Suika2Script, think of them as Suika2Script's vocabulary.

Suika2Script generally follows the same structure between commands and will almost always begin with an `@` symbol and include at least one or more arguments. Arguments range from numbers and file names to messages and configuration settings.

```
@command arguments
```

While this documentation currently provides basic usage examples, please see the [official website's command reference](https://suika2.com/en/reference.html) for full and advanced usage. This documentation may be updated to include advanced usage in the future.

# List of Commands
This list includes current and deprecated commands, please make sure you are using currently supported commands to avoid errors and broken scripts.

## @bg
This command displays or changes the background image, often called a 'bg'. After changing the background image, all character images will vanish from the stage. Background images are stored in the `bg/` folder. Please see [effects](#effects) for a list of effects that can be applied to background images.

This example displays a background image named `background.png` and fades it in over the course of `1.0 second`.

```
@bg background.png 1.0
```
From `Suika2.7.2`, HTML-like RGB color specifications (Hex Color) are supported.

```
@bg #ff8844 1.0
```

## @bgm
This command plays or changes the background music, often called 'bgm'. Background music is stored in the `bgm/` folder.

This example plays a background music file named `audio.ogg`.

```
@bgm audio.ogg
```

Background music also has a dedicated stop argument.

```
@bgm stop
```


## @ch
This command displays or changes the character, often called a 'ch'. Character images are stored in the `ch/` folder. Please see [positions](#positions) and [effects](#effects) for a list of positions and effects that can be applied to character images

This example displays a character file named `character.png` at the center of the screen and fades it in over the course of `1.0 second`.

```
@ch center character.png 1.0
```

Character images also have a dedicated hide argument that removes them from the screen, a duration in seconds can be appended to fade the character out.

```
@ch center none 1.0
```

To load a character image with alpha values, modify the following example:

```
@ch center character.png 1.0 n 0 0 show
```
As of `Suika2.11.3`, the `@ch` command now has the ability to display face-sprites with the `face` argument.

```
@ch face facesprite.png
```

Face-sprites are persistent, to hide face-sprites, append `none` to the `face` argument.

```
@ch face none
```

**Note:** You do not need to use the `none` argument when switching face-sprites, only the new face-sprite name is required.

## @cha
This command displays or changes a character animation, often called a 'cha'. Character animations are based on `@ch`, therefore its images are stored in the `ch/` folder. The `@cha` command can utilize the same [positions](#positions) and [effects](#effects) as `@ch`.

This example moves the center character image to the left by `600 pixels`. Notice that both the horizontal and vertical offset are included, and if one is unused, it should be set to `0`.

```
@cha center 1.0 move -600 0
```

`@cha` supports accelerating and decelerating the image towards the target offset or destination. To do so, append the term `accel` or `brake` after the duration argument.

```
@cha center 1.0 accel -600 0
```

As well as acceleration and deceleration, `@cha` also supports manipulation of image alpha values to adjust the transparency of a character image on the screen. To do so, it is important to load alpha values onto your images, to learn how to do this, please see [`@ch`](#ch).

Alternatively, it is possible to use the presets `show` which is the equivalent to an alpha value of `255` and `hide` which is the equivalent to an alpha value of `0`.

Append `show` or `hide` to the `@cha` command.

```
@cha center 1.0 accel -600 0 hide
```

## @chapter
This command appends a custom title to the window. By default, the chapter title is separated from the window title by a space, this can be changed from the [configuration file](/docs/2.-setup-and-configuration)

```
@chapter Chapter I: A Title
```

## @choose
This command shows options and jumps to the specified label. This is most often used when branching the story or offering an in-game choice to the player. You can create up to eight options with this command.

```
@choose label1 "I select the first option." label2 "I select the second option."
:label1
This is the first option.
@goto end
:label2
This is the second option.
:end
```

`@choose` allows custom label names, therefore `label1` and `label2` as well as `:label1` and `:label2` can be renamed to anything as long as they match. Please see [labels](#labels) for an explanation of labels.

## @chs
This command allows multiple character changes and also supports changing the background at the same time.

Please see [effects](#effects) for a list of effects that can be applied.

The character specification order is: `center`, `right`, `left` and `back`.

This command supports individual selectors, in this case `stay` deems the character unchanged. Durations in `seconds` can be appended after the character specification. A background image argument can also be appended along with an effect.

```
@chs none stay stay stay 1.0 background.png
```

## @click
This command instructs Suika2 to wait for a click before continuing. While waiting, the message box is hidden.

```
@click
```

## @gosub
This command is used to jump to a `subrutine`.

This command is not actually used in the creation of regular visual novels. You can safely ignore this command.

Use [`@return`](#return) to exit the subrutine.

```
@gosub SUB
:SUB
Describe the process here.
@return
```

## @goto
This command is used to jump to [labels](#labels).

A common usage of `@goto` is to jump from one section to another.

```
:a
This is simply an example, please ignore this message text.
@goto c

:b
This is simply another example, please ignore this message text too.
@goto end

:c
This is the final example, please ignore this message as well.
@goto end
```

A common usage of `@goto` is to create loops that contain variable checks. A basic loop can be written with a label, message, and `@goto`.

```
:abc
Describe the process here. This can include variable checks or 'question loops'.
@goto abc
```

Another common usage of @goto is to open special system menus, these include `$LOAD` and `$SAVE`.

```
@goto $LOAD
@goto $SAVE
```

## @gui
This is a special command, it was introduced in `Suika2.11.0` and is used to create graphical user interfaces such as menus and configuration screens. GUI files are stored in the `gui/` folder. While a basic example is provided, it is recommended that users see [5. The GUI File](/docs/4.-creating-graphical-user-interfaces) for a complete explanation on the way GUI files work. This file is a replacement of [`@menu`](#menu) and the deprocated [`@retrospect`](#retrospect).

`@gui` can be used for most menu-type screens including main menu screens, configuration screens, load screens, and save screens.

`@gui` supports 128 buttons on the screen at the same time. Some button types include `jump to label` and `show if variable is set`. `cancel` can be appended to the command to allow right-click cancel.

```
@gui menu.txt
```

## @if
This command jumps to the specified [label](#label) if the specified condition (a prerequisite) is true.

Please see [operators](#operators) for a list of operators that can be applied to `@if` commands.

```
@if $1 == 1 abc
Variable 1 is not 1. It means, the flag is not set.
Write story here.
@goto end
:abc
Variable 1 is 1. It means, the flag is set.
Write story here.
:end
Branched stories join here.
```

## Labels
This command creates a label, which can be used as a jump target. Please see [@goto](#goto) for an example of how to use labels.

## @load
This command acts in a similar way to [@goto](#goto). However, instead of just jumping to a label, it jumps to a different script.

```
@load scriptname.txt
```

## @menu
This command displays a menu using two images. You can create up to 16 buttons with `@menu`. `@menu` is an older alternative to the [GUI system](#gui). This works by displaying the first image by default with the second image being substituted in when you hover over a button. See the demo game for a helpful example! 

**Note:** Menus cannot be canceled using right click.

```
@menu menu.png menu-selected.png START 640 480 240 120 END 880 600 240 120
:START
:END
```

Please see this breakdown of the above script:

* `menu.png` – First image
* `menu-selected.png` – Second image
* `START` – Jump target label when button clicked
* `END` – Jump target label when button clicked
* `640 480` – Top-left position of button (`x`, `y`)
* `240 120` – Button size (`width`, `height`)

## Messages
This command prints text to the message box. You can use `\n` to insert a line feed and `$ + #` to show the value of a variable (e.g., `$1`)

```
Hello, I'm a message!
```

Messages can also include character names, these are displayed as text in the namebox.

```
*Midori*Hello, I'm Midori!
```

Messages can also include voice audio files.

```
*Midori*voice.ogg*Hello, I'm Midori!
```

An NVL-style can be achieved using line breaks.

```
The first line goes here\nthe second here.
```

There should be no spaces between the first text-part, `\n`, and the second text-part.



## @news
**Caution:** This command is not used by regular visual novels and is therefore not recommended.

This command is a variant of [`@switch`](#switch). It shows the first four options on the `north`, `east`, `west`, and `south` areas of the screen. The first eight options (parent options) are hidden when `*` is specified.

## @retrospect
This command executes "event image catalog mode". You could use this command to display the CG artwork that a player has unlocked. `@retrospect` is similar to [`@menu`](#menu) in its use of two images, however it has some more advanced parameter options such as:

* Background image.
* Background image (selected button).
* `RGB` values to fill buttons when the specified variable is 0.
* The width and height of buttons.
* For each button:
	* The jump target label.
	* The variable to check.
	* The X and Y position of the button.

The differences between `@menu` and `@retrospect` are:

* `@retrospect` can create up to twelve buttons.
* `@retrospect` can hook variables to buttons. If the value of the variable is `0`, the button is hidden.
* `@retrospect` can be canceled using right click.

Please see the below example:

```
@retrospect menu.png menu-selected.png 0 0 0 320 240 PICTURE1 $1 320 240
Canceled.
@goto END
:PICTURE1
@bg picture1.png 1.0
@click
:END
```

## @return
**Caution:** This command is not used by regular visual novels. This command is used to return from subroutines.

Please see [`@gosub`](#gosub).

## @se
This command plays or changes the sound effects, often called 'se'. Sound effects are stored in the `se/` folder.

This example plays a sound effect named `soundeffect.ogg`.

```
@se soundeffect.ogg
```

Sound effects also have a dedicated stop argument. Append `stop` to cancel currently playing sound effects.

```
@se stop
```

## @select
This command is deprecated, please use [`@choose`](#choose) instead.

## @set
This sets a value to a variable. Local variables are `$0` to `$9999`. Local variables are stored independently in each save file. Global variables are `$10000` to `$10999`. Global variables are stored commonly across all save data.

**Note:** All variables are `integers` and initially `0`.

Please see [operators](#operators).

```
@set $0 = 1
```

## @setsave
This command enables or disables the save and load screen, which is called by right-clicking while waiting for message click or option click. When you call `@goto $LOAD` or `@goto $SAVE`, `@setsave enable` is implicitly called.

## @shake
This command shakes the screen.

This example shakes the screen horizontally for `1.0 second` three times by `100 pixels`.

```
@shake horizontal 1.0 3 100
```

## @skip
This command enables or disables the ability to skip timed commands with user interaction. A good use for this command is displaying a brand or game logo during boot time. You can also use this command to enable and disable a user's ability to skip messages during sections of gameplay. If you wish to do this, however, you have to use `@setsave disable` to inhibit the save and load screen, as no-skip mode is not saved to the save file. In addition, do not use `@goto $LOAD` and `@goto $SAVE` while in no-skip mode, this is for similar reasons.

```
@skip enable
@skip disable
```

## @switch
This command shows two-level options. There are eight parent options and each parent option has eight child options.

**Note:** This command is not recommended. Use [`@choose`](#choose) instead.

This example shows two levels of two options, four in total.

```
@switch "Parent option 1" "Parent option 2" * * * * * * LABEL1 "Child option 1" LABEL2 "Child option 2" * * * * * * * * * * * * LABEL3 "Child option 3" LABEL4 "Child option 4" * * * * * * * * * * * *
:LABEL1
:LABEL2
:LABEL3
:LABEL4
```

## @video
This command plays a video file. Video files are stored in the `mov/` directory.

**Note:** macOS now supports video in the `QuickTime File Format (.mov or .qt)` and `MPEG-4 (.mp4)` formats as of `Suika2.11.4`.

**Note:** The `mov/` directory is not stored in the `data#.arc` file.

```
@video sample.wmv
```

## @vol
This command sets the sound volume. Soundtracks are, in lower case: `bgm` for background music, `voice` for voice, and `se` for sound effects. The sound volumes of these three tracks are stored in each local save file. You can change the volumes frequently for sound production purposes.

The initial values of all sound volumes are `1.0`. They can range from `0.0` to `1.0`. `0.0` means `muted` and `1.0` means `max`. If you want to set "global volumes" which are common between save files, you can specify the track name in UPPER CASE. Global volumes indicate volumes which are set in the game config screen. When you set the global volumes, set fading time to `0`.

**Note:** Actual output volume is "volume for save data × global volume".

```
@vol bgm 0.5 1.0
@wait 1.0
```

## @wait
This command instructs Suika2 to wait for a specified time. Input from the keyboard or mouse will interrupt `@wait` (thus allowing Suika2 to continue). If the previous command is a message, the message box is visible while waiting for input.

```
@wait 5.0
```

# Preset Properties
A number of commands accept preset arguments and variables, please see the below sections for tables containing this information.

For context-relevant information, see the [official website's command reference](https://suika2.com/en/reference.html).

## Effects

| Effect Type                                | Effect Name        | Abbrev. Name 1 | Abbrev. Name 2  |
|--------------------------------------------|--------------------|----------------|-----------------|
| fade/dissolve (alpha blending)             | normal             | `n`            | (not specified) |
| rule-based transition                      | rule:file-name     |                |                 |
| right curtain                              | curtain-right      | `curtain`      | `c`             |
| left curtain                               | curtain-left       | `cl`           |                 |
| up curtain                                 | curtain-up         | `cu`           |                 |
| down curtain                               | curtain-down       | `cd`           |                 |
| right slide                                | slide-right        | `sr`           |                 |
| left slide                                 | slide-left         | `sl`           |                 |
| up slide                                   | slide-up           | `su`           |                 |
| down slide                                 | slide-down         | `sd`           |                 |
| right shutter                              | shutter-right      | `shr`          |                 |
| left shutter                               | shutter-left       | `shl`          |                 |
| up shutter                                 | shutter-up         | `shu`          |                 |
| down shutter                               | shutter-down       | `shd`          |                 |
| clockwise wipe                             | clockwise          | `cw`           |                 |
| counterclockwise wipe                      | counterclockwise   | `ccw`          |                 |
| clockwise wipe (20 degrees stepped)        | clockwise20        | `cw20`         |                 |
| counterclockwise wipe (20 degrees stepped) | counterclockwise20 | `ccw20`        |                 |
| clockwise wipe (30 degrees stepped)        | clockwise30        | `cw30`         |                 |
| counterclockwise wipe (30 degrees stepped) | counterclockwise30 | `ccw30`        |                 |
| open eyes                                  | eye-open           |                |                 |
| close eyes                                 | eye-close          |                |                 |
| open eyes (vertical)                       | eye-open-v         |                |                 |
| close eyes (vertical)                      | eye-close-v        |                |                 |
| open slit                                  | slit-open          |                |                 |
| close slit                                 | slit-close         |                |                 |
| open slit (vertical)                       | slit-open-v        |                |                 |
| close slit (vertical)                      | slit-close-v       |                |                 |


## Positions

| Display Position | Target Name |
|------------------|-------------|
| Front Center     | center      |
| Right            | right       |
| Left             | left        |
| Back Center      | back        |

## Operators

| Condition Type | Meaning                                 |
|----------------|-----------------------------------------|
| `>`              | a > b (a is greater than b)             |
| `>=`             | a ≧ b (a is greater than or equal to b) |
| `==`             | a = b (a is equal to b)                 |
| `<=`             | a ≦ b (a is less than or equal to b)    |
| `<`              | a < b (a is less than b)                |
| `!=`             | a ≒ b (a is not equal to b)             |
