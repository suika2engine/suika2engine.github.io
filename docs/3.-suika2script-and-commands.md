---
title: 3. Suika2Script and Commands
---

[Return to Table of Contents](home)

Suika2 uses Suika2Script, a simple-to-use, feature-rich markup that acts as a game's building blocks.

## What is Suika2Script?
Suika2Script is the markup language used in the creation of Suika2 script files (often referred to as scenario files in a general sense). Being such a versatile markup, the user only needs to learn a few concepts before they are able to utilise the entire feature-set from displaying messages, images, playing sounds, music, and even building menus.

Suika2Script is an actively developed markup, therefore it is subject to changes alongside Suika2. Please refer to this document for information regarding new, updated, and deprecated commands and terms.

## Commands
Commands are the instructions that make up Suika2Script, think of them as Suika2Script's vocabulary.

Suika2Script generally follows the same structure between commands and will almost always begin with an `@` symbol and include at least one or more arguments. Arguments range from numbers and file names to messages and configuration settings.

```
@command arguments
```

# List of Commands
This list includes current and deprecated commands, please make sure you are using currently supported commands to avoid errors and broken scripts.
Deprecated commands are still documented for those who prefer older versions of Suika2 and for history's sake.

## `@bg`

This command fades in a new background image over a specified time in seconds.

Background image files need to be stored in the `bg` folder.
The hex RGB colour specifier (e.g., `#0088ff`) may be used instead of an image file name.

When `@bg` is run, the originally displayed background as well as any character images
are removed from the stage.
Game developers can use `@chs` instead of `@bg`
when they want to keep character images on the stage.

If you are looking for a way to keep your characters from disappearing when
you change the background, use the following command:

```
@chs stay stay stay stay 1.0 background.png
```

for more information, see [`@chs`](#chs).

Fade-ins are accompanied by transition effects.
Please see the list of effects below:

|Effect Type                                       |Effect Name              |Abbrev. Name 1           |Abbrev. Name 2           |
|--------------------------------------------------|-------------------------|-------------------------|-------------------------|
|fade/dissolve (alpha blending)                    |`normal`                 |`n`                      |(not-specified)          |
|rule-based transition                             |`rule:file-mame`         |                         |                         |
|right curtain                                     |`curtain-right`          |`curtain`                |`c`                      |
|left curtain                                      |`curtain-left`           |`cl`                     |                         |
|up curtain                                        |`curtain-up`             |`cu`                     |                         |
|down curtain                                      |`curtain-down`           |`cd`                     |                         |
|right slide                                       |`slide-right`            |`sr`                     |                         |
|left slide                                        |`slide-left`             |`sl`                     |                         |
|up slide                                          |`slide-up`               |`su`                     |                         |
|down slide                                        |`slide-down`             |`sd`                     |                         |
|right shutter                                     |`shutter-right`          |`shr`                    |                         |
|left shutter                                      |`shutter-left`           |`shl`                    |                         |
|up shutter                                        |`shutter-up`             |`shu`                    |                         |
|down shutter                                      |`shutter-down`           |`shd`                    |                         |
|clockwise wipe                                    |`clockwise`              |`cw`                     |                         |
|counterclockwise wipe                             |`counterclockwise`       |`ccw`                    |                         |
|clockwise wipe (20 degrees stepped)               |`clockwise20`            |`cw20`                   |                         |
|counterclockwise wipe (20 degrees stepped)        |`counterclockwise20`     |`ccw20`                  |                         |
|clockwise wipe (30 degrees stepped)               |`clockwise30`            |`cw30`                   |                         |
|counterclockwise wipe (30 degrees stepped)        |`counterclockwise30`     |`ccw30`                  |                         |
|open eyes                                         |`eye-open`               |                         |                         |
|close eyes                                        |`eye-close`              |                         |                         |
|open eyes (vertical)                              |`eye-open-v`             |                         |                         |
|close eyes (vertical)                             |`eye-close-v`            |                         |                         |
|open slit                                         |`slit-open`              |                         |                         |
|close slit                                        |`slit-close`             |                         |                         |
|open slit (vertical)                              |`slit-open-v`            |                         |                         |
|close slit (vertical)                             |`slit-close-v`           |                         |                         |
|rule (universal transition/1-bit)                 |`rule:<rule-file>`       |                         |                         |
|melt (universal transition/8-bit)                 |`melt:<rule-file>`       |                         |                         |

The `mask` Effect type was removed when the `rule` effect type was introduced.
The demo includes a `rule-mask.png` file as a rule image corresponding to `mask`, therefore `rule:rule-mask.png` works fine.

### Usage 1

The following script changes the background image to `sample.png` immediately.
```
@bg sample.png
```

### Usage 2

The following script changes the background image to `sample.png` with a 1.5 second fade-in time.
The applicable effect type is "fade/dissolve (alpha blending)", which is default.
```
@bg sample.png 1.5
```

### Usage 3

The following script changes the background image to `sample.png` with a 1.5 second fade-in time.
The applicable effect type is "right curtain", which is like closing a curtain in the right direction.
```
@bg sample.png 1.5 curtain-right
```

### Usage 4

The following script changes the background to the colour black with a 1.5 second fade-in time.
The applicable effect type is "close eyes", which is like closing eyes slowly.
```
@bg #000000 1.5 eye-close
```

### Usage 5

The following script changes the background image to `sample.png` with a 1.5 second fade-in time.
The applicable effect type is "rule (universal transition/1-bit)", which uses `rule-star.png` image for transition rule.
```
@bg sample.png 1.5 rule:rule-star.png
```

### Usage 6

The following script changes the background image to `sample.png` with a 1.5 second fade-in time.
The applicable effect type is "melt (universal transition/8-bit)", which uses `rule-star.png` image for transition rule.
```
@bg sample.png 1.5 melt:rule-star.png
```

## `@ch`

This command displays or changes a character image.

Character image files need to be stored in the `ch` folder.

Character layers include `center` (center front), `right`, `left`,
`back` (center back) and `face` (character face).

|Display Position		 |Target Name					|Target Name 2  		 |Abbrev. Name        |
|--------------------|--------------------|--------------------|--------------------|
|Front Center  	     |`center`            |`centre`            |`c`   			        |
|Right               |`right`    					|                    |`r`   			        |
|Left                |`left`    				  |                    |`l`   			        |
|Back Center         |`back`              |                    |`b`   			        |
|Face Icon           |`face`              |                    |`f`       	        |

The effect types are the same as "Showing a Background (`@bg`).

All characters are vertically aligned at the bottom of the screen.

### Usage 1

The following script displays `sample.png` immediately in the `front center` position.
Note that `center`/`centre` can be abbreviated as `c`.
```
@ch center sample.png
```

### Usage 2

The following script displays `sample.png` over the course of 0.5 seconds in the `front center` position.
The applicable effect type is "fade/dissolve (alpha blending)", which is default.
```
@ch center sample.png 0.5
```

### Usage 3

The following script removes the `front center` character over the course of 0.5 seconds.
The applicable effect type is "fade/dissolve (alpha blending)", which is default.
```
@ch center none 0.5
```

### Usage 4

The following script displays `sample.png` in 1.0 second in the `front center` position.
The applicable effect type is "clockwise wipe".
```
@ch center sample.png 1.0 clockwise
```

### Usage 5

The following script displays `sample.png` in 1.0 second in the `front center` position.
Here, `100 50` means offset, shifting the display position 50 pixels to the right and 50 pixels down.
`normal` means the effect type "fade/dissolve (alpha blending)".
```
@ch center sample.png 1.0 normal 100 50
```

### Usage 6

The following script displays `sample.png` in 1.0 second in the `front center` position with the alpha value `127` (50%).
The alpha value ranges from `0` to `255`.
For the alpha value specifier, game developers can write `show` as an equivalent to `255` or `hide` as `0`.
```
@ch center sample.png 1.0 normal 0 0 127
```

## Showing a Message

This feature serves as the basis for the descriptive aspects of a story.
It is not an atmark ("`@`") command that makes this feature possible.
Instead, a message is one line of text that does not begin with an atmark,
asterisk ("`*`") or colon ("`:`").

Messages are output to a message box.
As the descriptive part does not have the speaker's name,
the namebox is hidden while the message is showing.

A messages is displayed one character at a time.
When clicked in the middle, all characters are displayed at once.
When all characters are displayed, Suika2 goes into
"Click Waiting Mode for Message" and displays a click animation prompting click-input.
If a click is made while a click animation is being displayed,
Suika2 moves to the next command.

At any point during the display of a message,
unless the control key is pressed and the message is unread,
Suika2 moves to the next command.

The following format specifiers are available.

* `\n` ... Insert a new line.
* `$ + number` ... Print the value of the variable (e.g., `$1`).
* `\` at the beginning of the line ... Append the message to the previous message. (append mode)

Append mode is useful for the creation of full-screen style novels.

End users can use the return key and down key instead of left click.

### Usage 1

The following script prints `'Hello World'` to the message box.
```
Hello, world!
```

### Usage 2

The following script prints `'A somewhat longer way of... Greeting the world!'` to a (presumably) NVL-style message box.

```
A somewhat longer way of...\n\n
\greeting the world!
```

For more information regarding NVL-style, see [Creating an NVL](creating-an-nvl).

## Showing a Line

This feature shows the dialog of the story.
It is not an atmark command that makes this feature possible.
A line is one line of text that begin with an asterisk.

This command prints text to the message box and also the character name to the name box.
It consists of a character name, a message, and an optional voice file.
They are separated by asterisks, for example:
```
*name*dialog message
*name*voice.ogg*dialog message
```

The name appears before the message.

Audio starts playing when the name is displayed.
Normally, audio playback is stopped before moving on to the next command,
but this behaviour can be suppressed in `config.txt`.

If `@` is added to the beginning of a voice file name,
the audio playback is looped for the length of the message.

Other functions are exactly the same as [`@bg`](#bg).

## `@bgm`


This command plays background music.

BGM files need to be stored in the `bgm` folder.

Unless otherwise specified, BGM is played in a loop.
Game developers can use the `once` specifier to prevent BGM from looping.

The ability to crossfade multiple BGMs is not available.
Therefore, game developers may first need to fade out the old BGM and then fade in the new BGM.

### Usage 1

The following script plays `sample.ogg` as BGM and playback is looped.
```
@bgm sample.ogg
```

### Usage 2

The following script immediately stops the BGM.
```
@bgm stop
```

### Usage 3

The following script plays `sample.ogg` as BGM and playback is not looped.
```
@bgm sample.ogg once
```

### Usage 4 (Application)

The following sample script fades-out the BGM over the course of 2 seconds.
```
@vol bgm 0.0 2.0
@wait 2.0
@bgm stop
@vol bgm 1.0
```

## `@se`

This command plays a sound effect.

Sound effect files need to be stored in the `se` folder.

### Usage 1

The following script starts the playback of a sound effect file.
```
@se click.ogg
```

### Usage 2

The following script stops the playback of a sound effect file.
```
@se stop
```

### Usage 3

The following script loops a sound effect file.
```
@se sample.ogg loop
```

### Usage 4

The following script plays a sound effect file in the `voice` track.
This feature is available for checking voice-volume without a text message.
```
@se click.ogg voice
```

## `@vol`

This command changes the volume.

Suika2 has three independent sound tracks:

* `bgm` or `b` for the BGM track
* `voice` or `v` for the character voice track
* `se` or `s` for the sound effect track

The volume of the specified sound track will fade in/out for the specified time.
This command does not wait until the fade in/out is complete.
Combining with the [`@wait`](#wait) feature is useful if necessary.

`bgm`, `voice`, and `se` tracks are used for "local" volumes, there are also "global" volumes.
Local volumes are stored in each save data file.
Global volumes are common to all save data files.
The final volume will be a multiplication of the local and global volumes.
To specify the global volumes, game developers can use capital `BGM`, `VOICE` and `SE` tracks.
Since the global volume changes immediately, it is not possible to specify a fade time.

Both the value of local and global volumes range from 0.0 to 1.0.

### Usage 1

The following script sets the local volume of BGM to 0.5 (50%) over the course of 1.0 second.
```
@vol bgm 0.5 1.0
```

### Usage 2

The following script immediately sets the global volume of BGM to 0.5 (50%).
Usually, the global volumes are set from the config screen,
so the use of this feature is not as common.
```
@vol BGM 0.5 0
```

## `@choose`

This command shows options and lets the user choose one option,
then jumps to the label selected.

The `@choose` command can display up to eight options.
A minimum of one option must be displayed.

### Usage

The following script shows options.

```
@choose label1 "Good morning." label2 "Good afternoon." label3 "Good evening."
:label1
Good morning.
@goto end
:label2
Good afternoon.
@goto end
:label3
Good evening.
:end
```

## Defining a Label

This is a jump target inside a script.
It is not an atmark command, but one line of text that begins with a colon.

This command can be used as a jump target,
which is typically used with the [`@choose`](#choose) command,
[`@goto`](#goto) command,
[`@if`](#if) commands and
the [GUI](#gui) buttons.

### Usage

The following script creates a loop.
```
:JumpTarget
Show some messages.
@goto JumpTarget
```

## `@cha`

This command displays a character sprite in motion.

The `@cha` command translates one character sprite at a time
and can also change its alpha value.
Refer to the "Showing a Character Image (`@ch`)" section for details on how to specify the character position and alpha value.

Constant velocity (`move`), acceleration (`accel`) and deceleration (`brake`) can be used for translation.

`@cha` is an abbreviation of "character animation".

### Usage 1

The following script moves the character in the `front center` position 600 pixels to the left and hides it with an animation time of 1.0 second.
```
@cha center 1.0 move -600 0 hide
```

### Usage 2

The following script moves the character in the `front center` position 600 pixels to the left and hides it with an animation time of 1.0 second.
This script is the same as Usage 1, but this one uses acceleration.
```
@cha center 1.0 accel -600 0 hide
```

### Usage 3

The following script moves the character in the `front center` position 600 pixels to the left and hides it with an animation time of 1.0 second.
This script is the same as Usage 1, but this one uses deceleration.
```
@cha center 1.0 brake -600 0 hide
```

### Usage 4

The following script preloads the character image out of screen, then moves it into the screen.
```
@ch right sample.png 0 normal 600 0 hide
@cha right 2.0 move -600 0 show
```

## `@chs`

This command changes character sprites and the background image all at once.

The `@chs` command has the ability to change just the character(s) or just the background, or both.

The character specification order is `center`, `right`, `left` and `back`.
The effect types are the same as [`@bg`](#bg).

`chs` is an abbreviation of "change stage".

### Special Usage (Change Only Background)

The following script changes the background image while leaving the characters in place with a 1.0 second fade time.
```
@chs stay stay stay stay 1.0 background.png
```

### Usage 1

The following script changes the `front center` and `right` characters with a 1.0 second fade time.
Other characters will not be changed.
```
@chs center.png right.png stay stay 1.0
```

### Usage 2

The following script vanishes the character in the `front center` position with a 1.0 second fade time.
Other characters will not be changed.
```
@chs none stay stay stay 1.0
```

### Usage 3

The following script changes the background without any changes to the characters with a 1.0 second fade time.
```
@chs stay stay stay stay 1.0 background.png
```

### Usage 4

The following script changes the character in the front center position as well as the background with a 1.0 second fade time.
The effect type is "right curtain".
```
@chs center.png stay stay stay 1.0 background.png curtain-right
```

## `@shake`

This feature shakes the screen.
The command that accomplishes this feature is `@shake`.

The `@shake` command allows the screen to oscillate horizontally or vertically.
The oscillation is a "simple harmonic motion".
The period and the amplitude can be specified.

### Usage 1

The following script shakes the screen horizontally by 100px three times over a period of 1.0 second.
`horizontal` can be abbreviated as `h`.
```
@shake horizontal 1.0 3 100
```

### Usage 2

The following script shakes the screen vertically by 100px three times over a period of 1.0 second.
`vertical` can be abbreviated as `v`.
```
@shake vertical 1.0 3 100
```

## `@video`

This feature plays a video file.
The command that accomplishes this feature is `@video`.

The `@video` command plays a video file (if the file format is supported by the platform).
Available video formats vary from platform to platform,
but will be standardized to mp4 in the future.
At the moment, `.wmv` is preferred on Windows and `.mp4` is preferred on other platforms.
Note that on Linux, the end user must have the Gstreamer plugin which the game developer intended.

Video files are stored in the `mov` folder.
The `mov` folder is not stored in the `data01.arc` file.

**Note:** There is no limitation to the duration of videos, however longer video files will increase the overall size of your project and in some cases moderately-large or overly-large video files will cause unexpected behaviour. If you encounter an issue like this, please reach out.

### Usage

The following script plays a video file.
```
@video sample.avi
```

## `@gui`

This feature shows a graphical user interface (GUI).
The command that accomplishes this feature is `@gui`.

A GUI is a menu consisting of three images
and can show up to 128 buttons on the screen.
Button types include "jump to label", "show if variable is set", etc.

GUI definition files are also used for the config, save, load and history screens.

For further details, see ["4. Creating Graphical User Interfaces"](#4.-creating-graphical-user-interfaces) section.

### Usage 1

The following script shows GUI `menu.txt`.
```
@gui menu.txt
```

### Usage 2

The following script show GUI `menu.txt`.
It allows cancellation by right click.
```
@gui menu.txt cancel
```

## `@click`

This feature waits for a user click.
The command that accomplishes this feature is `@click`.

During auto mode, Suika2 resumes after a 2-second pause.

### Usage

The following script waits for a click.
```
@click
```

## `@wait`

This feature waits for a specified time in seconds.
The command that accomplishes this feature is `@wait`.

If a click or keystroke is made, the wait is canceled.

If the previous command is a message,
the message box is visible while waiting for input,
otherwise, the message box is hidden while waiting for input.

### Usage

The following script waits for 5 seconds.
```
@wait 5.0
```

## `@skip`

This feature enables/disables skip by control key and skip mode.
The command that accomplishes this feature is `@skip`.

The `@skip` command enables or disables the ability to skip timed commands by way of user interaction.
Typically, this command is used to display a brand logo on boot time.

In non-skippable mode,
mid-flight skipping of message display is inhibited,
but to use this,
you have to use `@setsave disable` to inhibit the save/load screen.
This is because the state of non-skippable mode is not recorded to the save data files.
In addition, don't use `@goto $LOAD` and `@goto $SAVE` while non-skippable mode is active.
The reason is the same as above.

### Usage 1

The following script enables skip. (default/skippable-mode)
```
@skip enable
```

### Usage 2

The following script disables skip. (non-skippable mode)
```
@skip disable
```

## `@goto`

This feature moves the script execution position to the specified label.
The command that accomplishes this feature is `@jump`.

Currently, there is no way to jump directly to labels in other scripts.

Suika2 behaves in a special way when `$LOAD` or `$SAVE` is specified as the label for the jump destination.

### Usage 1

The following script jumps to the label `abc` (thus creating a loop).
```
:abc
Describe the process here.
@goto abc
```

### Usage 2

The following script shows the load screen.
```
@goto $LOAD
```

### Usage 3

The following script shows the save screen.
```
@goto $SAVE
```

## `@set`

This feature sets variable values.
The command that accomplishes this feature is `@set`.

The `@set` command can perform calculations as well as simple assignments.
Addition, subtraction, multiplication and remainder are possible.
Available operators are:

* `=` (assignment)
* `+=` (addition)
* `-=` (subtraction)
* `*=` (multiplication)
* `/=` (division)
* `%=` (remainder)

On RHS, `$RAND` can be specified for a random number.

Local variables range from `$0` to `$9999`. They are stored independently in each save data file.
Global variables range from `$10000` to `$10999`. They are stored globally across all save data files.
All variables have 32-bit integer values and are initialized as `0`.

### Usage 1

The following script sets the value of `1` to the variable `$0`.
```
@set $0 = 1
```

### Usage 2

The following script adds the value `23` to the variable `$10`.
```
@set $10 += 23
```

### Usage 3

The following script assigns a random number (from 0 to 2147483647) to the variable `$0`.
```
@set $0 = $RAND
```

### Usage 4

The following script adds the value of the variable `$2` to the variable `$1`.
```
@set $1 += $2
```

## `@if`

This feature moves the script execution position to the specified label
if the specified condition of a preset variable is met.
The command that accomplishes this feature is `@if`.

Available operators are:

|Operator|Meaning                 |
|--------|------------------------|
|`>`     |greater than            |
|`>=`    |greater than or equal to|
|`==`    |equal to                |
|`<=`    |less than or equal to   |
|`<`     |less than               |
|`!=`    |not equal to            |

LHS must be a variable name (e.g., `$1`).

RHS must be an integer or a variable name.

### Usage

The following script jumps to the label `abc` if the variable at number 1 is equal to `1`.
```
@if $1 == 1 abc

Story when flag is not set.

@goto END
:abc

Story when flag is set.

@goto END
:END

Stories join here.
```

## `@load`

This feature loads a script file.
The command that accomplishes this feature is `@load`.

### Usage

The following script jumps to the script file `001.txt`.
```
@load 001.txt
```

## `@chapter`

This feature sets a chapter name.
The command that accomplishes this feature is `@chapter`.

The name of the chapter is reflected in the window title and saved data items.

### Usage

The following script sets the chapter name.
```
@chapter "Chapter 1"
```

## `@gosub`

This feature calls a subroutine.
The command that accomplishes this feature is `@gosub`.

Note that nested calls of subroutines are not supported.

### Usage

The following script runs subroutine `SUB`.
```
@gosub SUB
@goto END
:SUB
Describe the process here.
@return
:END
```

## `@return`

This feature returns from a subroutine.
The command that accomplishes this feature is `@return`.

### Usage

The following script runs subroutine `SUB`.
```
@gosub SUB
@goto END
:SUB
Describe the process here.
@return
:END
```

## Multilingualisation Prefix

This feature automatically switches the language to be displayed
depending on the user's system locale.

To use this feature, a script line must be started with a plus sign.

```
+en+Hello.
+fr+Bonjour.
```

This is not exactly a command, but it is a prefix to a command.
